    module Boot
boot:
    di
    im 1
    ld sp, #ffff

    call Display.init
    call Uart.init

    ld a, PAGE_OFFLOAD : call Memory.setPage
    ld hl, CBASE
    ld de, ccp_backup
    ld bc, FBASE-CBASE
    ldir

    ld a, PAGE_USER : call Memory.setPage
    call Drive.init

    call show_banner
    call IDEDOS_Volumes.init    

    ld c, 0 : call SELDSK
    xor a : ld (TDRIVE), a

    call Autoexec.try
    jr wboot.skip_move
wboot:
    di
    ld sp, #ffff
    ld a, PAGE_OFFLOAD : call Memory.setPage

    ld hl, ccp_backup
    ld de, CBASE
    ld bc, FBASE-CBASE
    ldir
.skip_move:
    ld a, PAGE_USER : call Memory.setPage
    
    ; Setup jump table
    ld a, #c3
    ld (0), a
    ld hl, WBOOT
    ld (1), hl
    
    ld (5), a
    ld hl, FBASE 
    ld (6), hl

    ld bc, #80
    call SETDMA
    
    ld a, 1 : ld (IOBYTE),a
    call BIOS.install_int

    call HOME
    im 1

    ld sp, cpm_start - 1
    
    ld a, (TDRIVE)
    ld c, a
    
    ei
    jp CBASE


show_banner:
    ld hl, banner
    jp BIOS.bios_print

    endmodule